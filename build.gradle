plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

group 'WildTools'
version = "2.11.0"

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        maven { url 'https://repo.bg-software.com/repository/nms/' }
    }

    shadowJar {
        archiveFileName = "${project.name}.jar"
        destinationDirectory = file("../archive/")
    }

    build {
        dependsOn shadowJar
    }
}

repositories {
    maven { url 'https://repo.bg-software.com/repository/nms/' }
    maven { url 'https://repo.bg-software.com/repository/api/' }
    maven { url 'https://repo.bg-software.com/repository/public-libs/' }

    String mavenUsername = project.hasProperty('mavenUsername') ? project.mavenUsername : System.getenv('mavenUsername');
    String mavenPassword = project.hasProperty('mavenPassword') ? project.mavenPassword : System.getenv('mavenPassword');

    if(mavenUsername != null && mavenPassword != null) {
        maven {
            url 'https://repo.bg-software.com/repository/private-libs/'
            credentials {
                username mavenUsername
                password mavenPassword
            }
        }
    }

}

dependencies {
    implementation project(":API")
    implementation fileTree("archive")

    // Spigot jars
    compileOnly "org.spigotmc:v1_7_R3:latest"
    compileOnly 'org.spigotmc:v1_16_R3-Tuinity:latest'
    // Plugin Hooks
    compileOnly "com.cloth:ChunkCollectors:latest"
    compileOnly "dev.warse:ChunkHoppers:latest"
    compileOnly "com.zrips:CMI:latest"
    compileOnly "com.earth2me:Essentials-2.16:latest"
    compileOnly "com.massivecraft:Factions:latest"
    compileOnly "com.massivecraft:MassiveCore:latest"
    compileOnly "com.drtshock:FactionsUUID:latest"
    compileOnly "com.drtshock:FactionsUUID-New:latest"
    compileOnly "net.brcdev:ShopGUIPlus-1.43:latest"
    compileOnly "net.prosavage:FactionsX:latest"
    compileOnly "net.prosavage:FactionsX-TNTAddon:latest"
    compileOnly "me.ryanhamshire:GriefPrevention:latest"
    compileOnly "com.pablo67340:GUIShop:latest"
    compileOnly "me.angeschossen:Lands:latest"
    compileOnly "com.nossr50:mcMMO-2:latest"
    compileOnly "com.vk2gpz:MergedSpawnerAPI:latest"
    compileOnly "me.swanis:SuperMobCoins:latest"
    compileOnly "com.newtjam:NewtShop:latest"
    compileOnly "su.fogus:FogusCore:latest"
    compileOnly "su.nightexpress:QuantumShop:latest"
    compileOnly "com.zrips:Residence:latest"
    compileOnly "de.dustplanet:SilkSpawners:latest"
    compileOnly "com.palmergames:Towny:latest"
    compileOnly "net.milkbowl:Vault:latest"
    compileOnly "com.stefthedev:Villages:latest"
    compileOnly "com.bgsoftware:WildChestsAPI:latest"
    compileOnly "com.bgsoftware:WildStackerAPI:latest"
    compileOnly "dev.rosewood:RoseStacker:latest"
}

jar {
    from {
        for (File file : new File("archive").listFiles()) {
            zipTree(file)
        }
    }
}

processResources {
    outputs.upToDateWhen {false}
    String versionNumber = System.getenv("BUILD_NUMBER") == null ? version : version + "-DEVb" + System.getenv("BUILD_NUMBER")
    eachFile { details ->
        if (details.name.contentEquals('plugin.yml')) {
            filter { String line ->
                line.replace('${project.version}', versionNumber)
            }
        }
    }
}

shadowJar {
    dependsOn(jar)

    if(System.getenv("BUILD_NUMBER") == null){
        archiveName = rootProject.name + ".jar"
    }
    else{
        archiveName = rootProject.name + "-b" + System.getenv("BUILD_NUMBER") + ".jar"
    }

    delete fileTree('./target/') {
        exclude archiveName
    }

    destinationDir = file("./target/")
    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]
}

task copyAPI(type: Copy) {
    from './archive/API.jar'
    into './target/'
    rename('API.jar', rootProject.name + 'API.jar')
}

clean {
    delete file('./archive/')
}

build {
    dependsOn shadowJar
    dependsOn copyAPI
    dependsOn clean
}

publish.shouldRunAfter shadowJar
shadowJar.shouldRunAfter build
build.shouldRunAfter subprojects.build